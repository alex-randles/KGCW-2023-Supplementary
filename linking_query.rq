PREFIX oscd: <https://w3id.org/OSCD#>
PREFIX rml: <http://semweb.mmlab.be/ns/rml#>
PREFIX rr: <http://www.w3.org/ns/r2rml#>

SELECT ?tripleMap ?source ?change ?reference ?changedData
WHERE {
  GRAPH ?changesGraph {    			 
	?changeLog a oscd:ChangeLog;
           	oscd:hasChange ?change;
   			oscd:hasCurrentVersion ?currentVersion .
	?change oscd:hasDataReference ?reference;
        	oscd:hasChangedData ?changedData .
	BIND (REPLACE(STR(?currentVersion), "^.*/([^/]*)$", "$1") as ?source)
  }
  GRAPH ?mappingGraph {    			 
	?tripleMap rml:logicalSource|rr:logicalTable ?logicalSource;
           	rr:predicateObjectMap ?pom .
	?logicalSource rml:source|rr:tableName ?source .
	?pom rr:objectMap ?objectMap .
	?objectMap rml:reference|rr:column ?reference.
  }
}
